# Main entrypoint of the workflow.
# Please follow the best practices:
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there.
start_years = list(range(1980, 2024, 5))
periods = [(str(start), str(min(start + 4, 2023))) for start in start_years]
# Possible values are 'NA' (North Atlantic), 'SA' (South Atlantic), 'EP' (Eastern North Pacific, which includes                                                the Central Pacific region), 'WP' (Western North Pacific), 'SP' (South Pacific), 'SI' (South Indian), 'NI' (North Indian).
basins = ["NA", "SA", "EP", "WP", "SP", "SI", "NI"]  # Replace with actual basin names

rule create_centroids:
    output:
        "global_centroids.hdf5"
    log:
        "logs/create_centroids.log"
    resources:
        mem_mb_per_cpu=12000
    conda:
        "climada_env"
    script:
        "scripts/create_centroids.py"

rule process_5_year_period:
    input:
        global_cent = "global_centroids.hdf5"
    output:
        "basins/{basin}_{start}_{end}.hdf5"
    wildcard_constraints:
        basin="[A-Z]+",
        start="[0-9]{4}",
        end="[0-9]{4}"
    conda:
        "climada_env"
    params:
        buf=5,
        timestep=0.5
    resources:
        mem_mb_per_cpu=12000
    log:
        "logs/create_{basin}_{start}_{end}.log"
    script:
        "scripts/create_basin_5_year_period.py"

rule concatenate_all_periods:
    input:
        expand("basins/{{basin}}_{start}_{end}.hdf5", zip, start=[p[0] for p in periods], end=[p[1] for p in periods])
    output:
        "basins/{basin}_full_1980_2023.hdf5"
    wildcard_constraints:
        basin="[A-Z]+"
    conda:
        "climada_env"
    log:
        "logs/concatenate_{basin}.log"
    script:
        "scripts/concatenate_all_periods.py"

rule concatenate_all_basins:
    input:
        expand("basins/{basin}_full_1980_2023.hdf5", basin=basins)
    output:
        "basins/all_basins_1980_2023.hdf5"
    conda:
        "climada_env"
    log:
        "logs/concatenate_all_basins.log"
    script:
        "scripts/concatenate_all_basins.py"
